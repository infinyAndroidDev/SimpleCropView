apply plugin: 'com.android.library'

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android {
  compileSdkVersion 29
  buildToolsVersion '28.0.3'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 29
    versionCode 1
    versionName "1.0.0"
    consumerProguardFiles 'proguard-rules.pro'
  }

  dexOptions {
    preDexLibraries = false
  }
}

def file = project.rootProject.file('local.properties')
if (!file.exists()) return

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.mayank'
version = '1.0.0'
def siteUrl = 'https://github.com/mayank1513/SimpleCropView'
def gitUrl = 'https://github.com/mayank1513/SimpleCropView.git'
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives'] //When uploading configuration files
  pkg {
    repo = 'SimpleCropView'
    name = 'com.mayank.SimpleCropView'
    desc = 'A simple lightweight image cropping library for Android. Adds less than 50kb to your apk'
    websiteUrl = siteUrl
    issueTrackerUrl = 'https://github.com/mayank1513/SimpleCropView/issues'
    vcsUrl = gitUrl
    licenses = ['MIT']
    labels = ['android', 'image-crop', 'android-library']
    publicDownloadNumbers = true
  }
}

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging 'aar'
        name 'SimpleCropView'
        url siteUrl
        licenses {
          license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id 'mayank1513'
            name 'Mayank Kumar Chaudhari'
            email 'mayank.srmu@gmail.com'
          }
        }
        scm {
          connection 'https://github.com/mayank1513/SimpleCropView.git'
          developerConnection 'https://github.com/mayank1513/SimpleCropView.git'
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set("sources")
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}

task findConventions doLast {
  println project.getConvention()
}
